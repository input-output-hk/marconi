[#contract-api]
= Writing Plutus Contracts

The `PlutusContract` type represents the client-side parts of Plutus contracts. `PlutusContract` values describe the interactions of contracts with the blockchain and with the user. They can be composed in different ways, allowing you to assemble contracts from the building blocks provided by the Plutus library and by other contract authors.

When you have a `PlutusContract` you can compile it to a contract bundle (a format suitable for distributing contracts to end users).

== Simple contracts

[source,haskell]
----
{-# LANGUAGE TypeApplications #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE MonoLocalBinds   #-}
module ContractAPI where
import           Language.Plutus.Contract
import           Language.Plutus.Contract.Tx (UnbalancedTx)
import           Ledger.AddressMap           (AddressMap)
import           Ledger.Crypto               (PubKey)
import           Ledger.Slot                 (Slot)
----

The type of contracts is `Contract r a`. It has two type parameters: `r` describes the effects that the contract has, and `a` is the return type of the contract. You can think of `r` as a list of capabilities, or permissions, that the contract must be allowed to do in order to function. Commonly `r` contains effects such as producing transactions, collecting input from users, and watching the blockchain for changes.

The simplest contract is the one that produces no transactions, and does not interact with the user or the wallet at all:

[source,haskell]
----
c1 :: Contract r () -- <1>
c1 = return () -- <2>
----
<1> The type of `c1` is a Plutus contract that produces a unit value when it's finished. The fact that `r` is free tells us that `c1` does not have any effects.
<2> The implementation of `c1` returns the unit immediately.

== Common effects

If we compiled `c1` and deployed it, it would finish right away without doing anything. To get the contract to interact with the environment we can use the functions exported by the `Language.Plutus.Contract` module.

=== Waiting

 For example, `awaitSlot {2c} (Member AwaitSlot r) => Slot -> PlutusContract r Slot` is a function that takes a `Slot` argument and produces a contract that waits until the slot has been reached. The `Member AwaitSlot r` constraint tells us that `awaitSlot` has the `AwaitSlot` effect. If we want to make use of `awaitSlot` in our own contract, we need to include the `Member AwaitSlot r` constraint in our contract's signature. This ensures that we can see all the effects that a contract may have just from its type signature, without having to look at its implementation.

[source,haskell]
----
c2 :: (Member AwaitSlot r) => Contract r Slot
c2 = awaitSlot 2765 -- <1>
----
<1> `c2` waits until slot 2765 and then returns the current slot.

=== Blockchain Events

Smart contracts often involve multiple participants that synchronise their actions over the blockchain. To be notified of changes to the contract state we need the `WatchAddress` effect. It lets us watch any address on the blockchain for transactions that modify them. `Language.Plutus.Contract` contains a couple of basic functions for watching addresses.

`nextTransactionAt {2c} (Member WatchAddress r) => Address -> Contract r Tx` waits for the next transaction that changes the outputs at the given address, either by spending an output or by producing a new output. It returns the entire transaction.

Sometimes we want to see what outputs accumulate at an address, and spend all of them after a while. `watchAddressUntil {2c} (Member AwaitSlot r, Member WatchAddress r) => Address -> Slot -> Contract r AddressMap` keeps track of all changes to the address that happen between now and the given slot. It returns a value of `AddressMap` - a collection of outputs that can be used in transactions of the contract.

=== Transactions

Contracts write transactions using `writeTx {2c} (Member WriteTx r) => UnbalancedTx -> Contract r ()`. The transactions produced that way are unbalanced and unsigned. When executing a `writeTx` instruction, the app platform forwards the transaction to the user's wallet to balance it (by adding appropriate public key inputs or outputs) and compute the gas cost of any scripts involved. The finished transaction is then signed by the signing process, and submitted to the wallet which sends it to the blockchain. `writeTx` returns when the transaction has been submitted to the blockchain.

NOTE: In the future, `writeTx` will return some information about the transaction (status, gas fee, etc).

=== Endpoints

Many contracts require information from the end user. For example, if we send money somewhere then we need to know the amount and the address of the recipient. We might also want to inform the user that something has happened - that we sent the money, or how much time is left until we can unlock a value that is secured by a Plutus script.

Such interactions are realised using _endpoints_. You can think of endpoints as pieces of a user interface that display information and may take input in the form of text boxes, buttons, etc.

Let's say we write a contract that expects the user to enter an `Int` using an endpoint called "amount".

[source,haskell]
----
c3 :: (Member ExposeEndpoint r) => Contract r Int -- <1>
c3 = endpoint "amount" -- <2>
----
<1> Endpoints are governed by the `ExposeEndpoint` effect. 
<2> When using `endpoint` we need to provide a value of `String`, identifying the endpoint.

How does the `endpoint` function know what type of data the user is supposed to enter here? In the example `c3`, the return type (`Int`) helps the Haskell compiler to infer the type of the endpoint. Sometimes the compiler cannot figure out the type on its own. In that case we can provide a type annotation directly at the `endpoint` call:

[source,haskell]
----
c4 :: (Member ExposeEndpoint r) => Contract r Int
c4 = endpoint @Int "amount" -- <1>
----
<1> The `@Int` argument tells the compiler that an `Int` is expected by this endpoint. This notation is enabled by the `TypeApplications` language extension.

WARNING: The type of the `endpoint` function is going to change in order to reflect the schema (all endpoints of a contract will be known at compile time, and the schema is a representation of their types in a format that can be consumed by the app platform).

=== The `ContractActions` constraint

By writing out the endpoints that our contracts have we ensure that our contracts are predictable - even if we don't know what a concrete instance of the contract will look like, we know for certain that it is never going to ask you for something that isn't specified in the type.

We've seen the four effects that most smart contracts need - `AwaitSlot`, `WatchAddress`, `WriteTx` and `ExposeEndpoint`. To avoid having to write out the complete list of effects on the type signatures of our contracts, we can use the `ContractActions` constraint from `Language.Plutus.Contract`. It groups the four basic effects together, so we can write `ContractActions r => Contract r ()` . 

== Combining contracts

Given two contracts we can combine them by running them in parallel, in sequence, or by selecting the one that finishes first.

Let's say we have two contracts `collect {2c} Contract r AddressMap` and `recipient {2c} Contract r PubKey'`. `collect` watches the blockchain for payments to a script address, and after a while returns an address map with all the inputs that are currently there. `recipient` asks the user for an address to make the payment to. Now we would like to build a contract that combines `collect` and `recipient` and then submits a transaction that spends all inputs and pays the value to the given address.

=== Parallel

`collect` and `recipient` can run in parallel because neither of them depends on the other:

[source,haskell]
----
collect :: ContractActions r => Contract r AddressMap
collect = undefined

recipient :: ContractActions r => Contract r PubKey
recipient = undefined

collectRec :: ContractActions r => Contract r (AddressMap, PubKey) -- <1>
collectRec = both collect recipient -- <2>
----
<1> `collectRec` is a contract that may use an endpoint asking for a public key. It returns two things: A list of transaction inputs and a public key.
<2> `collectRec` is implemented in terms of `collect` and `recipient`, using `both` to run both contracts at the same time.

NOTE: `both` is defined as `liftA2 (,)`: Parallel composition is using the applicative instance of `Contract`.

=== Sequential

After having obtained the inputs and the public key we can proceed to produce the transaction.

[source,haskell]
----
mkTx :: AddressMap -> PubKey -> UnbalancedTx
mkTx = undefined

spend :: ContractActions r => Contract r ()
spend = do -- <1>
    (ins, pk) <- collectRec
    writeTx (mkTx ins pk)
----
<1> We use Haskell's do notation to signal the start of a sequence of actions

In the definition of `spend`, we run the `collectRec` contract from above and then pattern match on its result to get the `ins` and `pk` variables. After that we produce the transaction with `writeTx`.

NOTE: The monad instance of `Contract` is used for sequential composition of contracts.

=== Select

What if a contract involves an alternative? Let's say we have a contract that represents a portfolio of stocks, and at any point in time the user can increase or decrease the number of shares. We represent those decisions with two types:

[source,haskell]
----
data Buy = Buy { buySymbol :: String, buyAmount :: Int }
data Sell = Sell { sellSymbol :: String, sellAmount :: Int }
----

Then we define two contracts, `buy {2c} Contract r Buy` and `sell {2c} Contract r Sell`. Now the combined contract is

[source, haskell]
----
buy :: ContractActions r => Contract r Buy
buy = undefined

sell :: ContractActions r => Contract r Sell
sell = undefined

buyOrSell :: ContractActions r => Contract r (Either Buy Sell)
buyOrSell = selectEither buy sell
----

The `selectEither` combinator takes two contracts with return types `a` and `b`, and produces a new contract with return type `Either a b` that produces the outcome of the branch that finished first.

NOTE: The `Alternative` instance of `Contract` is used to select one of two branches.

== Contract instances

Each `PlutusContract` describes a large set of possible paths that the contract can take. Each of those paths is an _instance_ of that contract. Instances are sequences of events that the contract reacts to, and the transactions it produces. For example, if you offer a loan to somebody in exchange for collateral, then that loan would be an instance of the `loan` contract.

Instances are client-specific. There is one instance of `loan` running on your machine, going through the lender branch, and another instance on the recipient's machine, following the borrower's side of the contract.

The app platform that executes contract bundle keeps track of the running instances.

== State

Conceptually the state of a contract instance is the sequence of events that it has seen so far. In reality we don't want to store all the events of the instance, because there might be many of them, and if we wanted to restore the state by replaying the events it would take longer and longer the more events there are.

To avoid keeping old events around for longer than necessary we can use the `jsonCheckpoint` function.

`jsonCheckpoint` is a unary operator that takes a `PlutusContract` with a result that can be written to and read from JSON. The bookeeping system that is used behind the scenes to keep track of contract state will, upon encountering a contract wrapped in `jsonCheckpoint`, run the contract once and then store the result of that contract as a JSON object. The next time we restore the contract's state, the system will _not_ replay the events for that contract, but instead use the `FromJSON` instance to restore the state.

NOTE: Contracts that don't use `jsonCheckpoint` are still able to have their state saved and restored. This will take the form of the `[Event]` sequence of inputs that have been seen so far. 

NOTE: To handle things like the loop in the `sharedealing` example we probably need something more explicit, like a notion of cells that can be written to and read from. But we could implement that in the same manner as the `jsonCheckpoint` (the important bit is how the JSON constraints are embedded in the contract definition)